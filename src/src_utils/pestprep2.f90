!     Last change:  JD    7 Oct 2006   12:23 pm
program pestprep2

! -- Program PESTPREP2 adds observations to an existing PEST control file.
!    These observations are based on an observation bore sample file and a
!    complementary MOD2OBS-generated bore sample file.


	use defn
	use inter

	implicit none

	integer, parameter :: MAXPAR=5000

	logical :: active,lopened
	integer	:: ifail,sampunit1,sampunit2,iobs,nobs,maxobs,iline,idate, &
	           nbore,ibore,ierr,cols,jindex,i,j,ii1,ii2,jj1,jj2,nnchar, &
	           insunit,blankflag,nnnchar,npar,   &
	           j1,j2,ipar,ll,jj,kk,dd,ss,iichar,npargp,iheader,nbb
        integer :: nprefix,inunit,outunit
        integer :: nnobs,nprior,nobsgp,ntplfle,ninsfle
	integer :: numpargp(27)
	integer, allocatable    :: numsamp(:),bore_index(:),ndays(:),nsecs(:)
	real, allocatable       :: value(:)
	double precision        :: dtemp
	character (len=200)	:: sampfile1,sampfile2,specfile,insfle,   &
                                   afile,bfile,modcom
        character (len=200)     :: infile,outfile
	character (len=10)	:: abore,aboreold
        character (len=8)       :: aprefix
	character (len=6)	:: aline
	character (len=10), allocatable    :: boreid(:)
	character (len=1)       :: ause,aid,pardelim,achar
	character (len=12)      :: aachar,atemp,aapar,apar(MAXPAR)
        character (len=12)      :: agroup
        character (len=20)      :: aobs
	character (len=15)      :: afmt
	character (len=100)	:: aprompt
	character (len=2000)    :: ccline


	write(amessage,5)
5       format(' Program PESTPREP2 adds observations to an existing PEST input dataset. ',    &
	'These observation are based on a bore sample file prepared by MOD2OBS or SMP2SMP.')
	call write_message(leadspace='yes',endspace='yes')

        call read_settings(ifail,idate,iheader)
        if(ifail.eq.1) then
          write(amessage,7)
7         format(' A settings file (settings.fig) was not found in the ', &
          'current directory.')
          call write_message
          go to 9900
        else if(ifail.eq.2) then
          write(amessage,8)
8         format(' Error encountered while reading settings file settings.fig')
          call write_message
          go to 9900
        endif
        if((idate.ne.0).or.(datespec.eq.0)) then
          write(amessage,9)
9         format(' Cannot read date format from settings file ', &
          'settings.fig')
          call write_message
          go to 9900
        end if

	call readfig(specfile,bore_coord_file,sampfile1)

70	call open_named_input_file(ifail, &
	' Enter name of observation bore sample file: ',sampfile1,sampunit1)
	if(ifail.ne.0) go to 9900
	if(escset.ne.0) go to 9900


80	call open_input_file(ifail, &
	' Enter name of    model    bore sample file: ',sampfile2,sampunit2)
	if(ifail.ne.0) go to 9900
	if(escset.ne.0)then
	  escset=0
	  close(unit=sampunit1)
	  write(6,*)
	  go to 70
	end if	

	blankflag=0
	aboreold=' '
	nobs=0
	maxobs=0
	iobs=0
	iline=0
	nbore=0
	do
	  iline=iline+1
	  read(sampunit2,'(a)',end=200,err=9000) cline
	  call linesplit(ifail,1)
	  if(ifail.lt.0) then
	    blankflag=1
	    cycle
	  end if
	  if(blankflag.eq.1) then
	    call num2char(iline-1,aline)
	    write(amessage,90) trim(aline),trim(sampfile2)
90	    format(' Line ',a,' of model-generated bore sample file ', &
	    a,' is blank: thus it can''t have been generated by ',&
	    'MOD2OBS or SMP2SMP.')
	    go to 9890
	  end if
	  abore=cline(left_word(1):right_word(1))
	  call casetrans(abore,'hi')
	  nobs=nobs+1
	  iobs=iobs+1
	  if(abore.ne.aboreold)then
	    if(iobs-1.gt.maxobs)maxobs=iobs-1
	    aboreold=abore
	    iobs=1
	    nbore=nbore+1
	  end if
	end do
200	if(iobs.gt.maxobs)maxobs=iobs

!	open(unit=50,file='debug.dat')
!	write(50,*) ' maxobs = ',maxobs
!	write(50,*) ' nobs   = ',nobs
!	write(50,*) ' nbore  = ',nbore
!	close(unit=50)

! -- Next we re-read the model bore sample file to ascertain bore_identifiers.

	allocate(boreid(nbore),bore_index(nbore),numsamp(nbore), &
	ndays(nobs),nsecs(nobs),value(nobs),stat=ierr)
	if(ierr.ne.0) go to 9100
	numsamp=0
	value=-1.1e37

	rewind(unit=sampunit2,iostat=ierr)
	if(ierr.ne.0)then
	  write(amessage,250) trim(sampfile2)
250	  format(' Cannot re-wind bore sample file ',a)
	  go to 9890
	end if
	aboreold=' '
	ibore=0
	jindex=0
	iline=0
	do
	  iline=iline+1
	  read(sampunit2,'(a)',end=300,err=9000) cline
	  call linesplit(ifail,5)
	  if(ifail.lt.0) cycle
	  if(ifail.eq.0)then
	    call num2char(iline,aline)
	    write(amessage,270) trim(aline),trim(sampfile2)
270	    format(' Line ',a,' of model-generated bore sample file ', &
	    a,' has 5 entries: thus it can''t have been generated by ',&
	    'MOD2OBS or SMP2SMP.')
	    go to 9890
	  end if
	  call linesplit(ifail,4)
	  if(ifail.ne.0)then
	    call num2char(iline,aline)
	    write(amessage,280) trim(aline),trim(sampfile2)
280	    format(' Line ',a,' of model-generated bore sample file ', &
	    a,' has less than 4 entries: this can''t have been generated by ', &
	    'MOD2OBS or SMP2SMP.')
	    go to 9890
	  end if
	  cols=4
	  jindex=jindex+1
	  abore=cline(left_word(1):right_word(1))
	  call casetrans(abore,'hi')
	  if(abore.ne.aboreold)then
	    aboreold=abore
	    ibore=ibore+1
	    boreid(ibore)=abore
	    bore_index(ibore)=jindex
	  end if
	  numsamp(ibore)=numsamp(ibore)+1
	  call read_rest_of_sample_line(ifail,cols,ndays(jindex),nsecs(jindex), &
	  dtemp,iline,sampfile2)
	  if(ifail.ne.0) go to 9900
	end do
300	continue

!	open(unit=50,file='debug.dat')	!debug
!	do i=1,nbore                    !debug
!	  write(50,*) trim(boreid(i)), bore_index(i), numsamp(i)
!	end do                          !debug
!	write(50,*)                     !debug
!	do i=1,nobs                     !debug
!	  write(50,*) ndays(i),nsecs(i) !debug
!	end do                          !debug
!	close (unit=50)	                !debug

305	write(6,*)
362     write(6,364,advance='no')
364     format(' Enter prefix for new observation names (8 chars or less): ')
        read(5,'(a)') aprefix
        if(aprefix.eq.' ') go to 362
        aprefix=adjustl(aprefix)
        atemp=aprefix
        call casetrans(aprefix,'lo')
        if(atemp(1:2).eq.'e ') then
          close(unit=sampunit2,err=9150)
	  deallocate(boreid,bore_index,numsamp,ndays,nsecs,value,stat=ierr)
	  if(ierr.ne.0) go to 9200
	  write(6,*)
	  go to 80
        end if
        nprefix=len_trim(aprefix)
310     write(6,320,advance='no')
320     format(' Use numbers or bore identifiers for rest of observation ', &
        'names?  [n/b]: ')
        read(5,'(a)') ause
        if(ause.eq.' ') go to 310
        if(index(eschar,ause).ne.0) then
          write(6,*)
          go to 362
        end if
        call casetrans(ause,'lo')
        if((ause.ne.'n').and.(ause.ne.'b')) go to 310
329     continue
        if(ause.eq.'b')then
          if(maxobs.gt.99999)then
            write(amessage,330)
330         format(' There are too many observations for this option.')
            call write_message
            go to 310
          end if
          if(maxobs.gt.100000)then
            nnnchar=6
          else if(maxobs.gt.10000)then
            nnnchar=5
          else if(maxobs.ge.1000)then
            nnnchar=4
          else if(maxobs.ge.100)then
            nnnchar=3
          else if(maxobs.ge.10)then
            nnnchar=2
          else
            nnnchar=1
          end if
          nnchar=20-nprefix-nnnchar-1
          write(aachar,'(i8)') nnchar
          aachar=adjustl(aachar)
340       write(6,350,advance='no') trim(aachar),trim(aachar)
350       format(' Use first ',a,' or last ',a,' characters ',      &
          'of bore identifier?  [f/l]: ')
          read(5,'(a)') aid
          if(aid.eq.' ') go to 340
          if(index(eschar,aid).ne.0) go to 305
          call casetrans(aid,'lo')
          if((aid.ne.'f').and.(aid.ne.'l')) go to 340
	  if(nbore.ne.1)then
            if(aid.eq.'f') then
              do i=1,nbore-1
                do j=i+1,nbore
                  if(boreid(i)(1:nnchar).eq.boreid(j)(1:nnchar)) then
                    write(amessage,360)
360                 format(' This will not result in unique observation ', &
                    'names.')
                    call write_message(leadspace='yes',endspace='yes')
                    go to 362
                  end if
                end do
              end do
            else
              do i=1,nbore-1
                do j=i+1,nbore
                  jj1=len_trim(boreid(i))
                  ii1=max(1,jj1-nnchar+1)
                  jj2=len_trim(boreid(j))
                  ii2=max(1,jj2-nnchar+1)
                  if(boreid(i)(ii1:jj1).eq.boreid(j)(ii2:jj2))then
                    write(amessage,360)
                    call write_message(leadspace='yes',endspace='yes')
                    go to 362
                  end if
                end do
	      end do
	    end if
	  end if
	end if
370     write(6,372,advance='no')
372     format(' Enter name for new observation group: ')
        read(5,'(a)') agroup
        if(agroup.eq.' ') go to 370
        call casetrans(agroup,'lo')
        if(agroup(1:2).eq.'e')then
          write(6,*)
          if(ause.eq.'b')then
            go to 329
          else
            go to 310
          end if
        end if

400	write(6,*)
	call open_output_file(ifail,  &
        ' Enter name for instruction file: ', insfle,insunit)
	if(escset.ne.0)then
	  escset=0
	  write(6,*)
	  go to 310
	end if
	jindex=0
	write(insunit,405)
405	format('pif #')
	do i=1,nbore
	  do j=1,numsamp(i)
	    jindex=jindex+1
	    if(ause.eq.'n')then
	      call num2char(jindex,aobs)
              aobs=trim(aprefix)//trim(aobs)
	    else
              if(aid.eq.'f')then
                ii1=1
                jj1=min(nnchar,len_trim(boreid(i)))
              else
                jj1=len_trim(boreid(i))
                ii1=max(1,jj1-nnchar+1)
              end if
              write(afmt,410) nnnchar,nnnchar
410           format('(i',i5,'.',i5,')')
              write(atemp,afmt) j
              atemp=adjustl(atemp)
              aobs=trim(aprefix)//boreid(i)(ii1:jj1)//'_'//trim(atemp)
            end if
	    call casetrans(aobs,'lo')
            write(insunit,420,err=9300) trim(aobs)
420	    format('l1   [',a,']39:56')
          end do
        end do
	close(unit=insunit)
	write(amessage,430) trim(insfle)
430	format(' - file ',a,' written ok.')
	call write_message

! -- The names of the old and new PEST control files are acquired.

        write(6,*)
500     aprompt=' Enter name of existing PEST control file: '
        call open_input_file(ifail,aprompt,infile,inunit)
        if(ifail.ne.0) go to 9900
	if(escset.ne.0)then
	  escset=0
	  go to 400
	end if
520     call open_output_file(ifail, &
        ' Enter name for new PEST control file: ',outfile,outunit)
        if(ifail.ne.0) go to 9900
        if(escset.ne.0)then
          escset=0
          close(unit=inunit)
          go to 500
        end if

! -- Next we find out the measured values pertaining to all observations.

	iline=0
	aboreold=' '
	active=.false.
	read_meas_sample_file: do
	  iline=iline+1
	  read(sampunit1,'(a)',end=650) cline
	  call linesplit(ifail,1)
	  if(ifail.lt.0) cycle read_meas_sample_file
	  abore=cline(left_word(1):right_word(1))
	  call casetrans(abore,'hi')
	  if(abore.eq.aboreold)then
	    if(.not.active) cycle read_meas_sample_file
	  else
	    aboreold=abore
	    do i=1,nbore
	      if(abore.eq.boreid(i)) go to 620
	    end do
	    active=.false.
	    cycle read_meas_sample_file
620	    active=.true.
	  end if
	  call linesplit(ifail,4)
	  if(ifail.gt.0)then
	    call num2char(iline,aline)
	    write(amessage,630) trim(aline),trim(sampfile1)
630	    format(' Insufficient entries on line ',a,' of bore ', &
	    'sample file ',a)
	    go to 9890
	  end if
	  cols=4
	  call read_rest_of_sample_line(ifail,cols,dd,ss,dtemp,iline,sampfile1)
	  if(ifail.ne.0) go to 9900
	  do j=bore_index(i),bore_index(i)+numsamp(i)-1
	    if((dd.eq.ndays(j)).and.(ss.eq.nsecs(j)))then
	      value(j)=dtemp
	      if(j.ne.nobs)then
	        if(value(j+1).gt.-1.0e37)then
	          write(amessage,635) trim(sampfile1),trim(sampfile2)
635	          format(' Samples in file ',a,' are in a different order ', &
	          'from those in file ',a,': this is not possible if the ',  &
	          'latter was built from the former using MOD2OBS or SMP2SMP.')
                  write(6,*) 'iline = ',iline   !debug
	          go to 9890
	        end if
	      end if
	      go to 640
	    end if
	  end do
640	  continue
	end do read_meas_sample_file
650	continue

! -- The first part of PEST control file is read and written.

        iline=1
        read(inunit,'(a)',err=9600,end=9650) cline
        cline=adjustl(cline)
        call casetrans(cline,'lo')
        if(cline(1:3).ne.'pcf')then
          write(amessage,530) trim(infile)
530       format(' "pcf" header expected on first line of PEST control ',  &
          'file ',a,'.')
          go to 9890
        end if
        write(outunit,'(a)') trim(cline)
        do i=1,2
          iline=iline+1
          read(inunit,'(a)',err=9600,end=9650) cline
          write(outunit,'(a)') trim(cline)
        end do
        iline=iline+1
        read(inunit,'(a)',err=9600,end=9650) cline
        call linesplit(ifail,5)
        if(ifail.ne.0) go to 9600
        npar=char2int(ifail,1)
        if(ifail.ne.0) go to 9600
        nnobs=char2int(ifail,2)
        if(ifail.ne.0) go to 9600
        npargp=char2int(ifail,3)
        if(ifail.ne.0) go to 9600
        nprior=char2int(ifail,4)
        if(ifail.ne.0) go to 9600
        nobsgp=char2int(ifail,5)
        if(ifail.ne.0) go to 9600
        write(outunit,540) npar,nnobs+nobs,npargp,nprior,nobsgp+1,trim(cline(right_word(5)+1:))
540     format(5i7,4x,a)
        iline=iline+1
        read(inunit,'(a)',err=9600,end=9650) cline
        call linesplit(ifail,2)
        if(ifail.ne.0) go to 9600
        ntplfle=char2int(ifail,1)
        if(ifail.ne.0) go to 9600
        ninsfle=char2int(ifail,2)
        if(ifail.ne.0) go to 9600
        write(outunit,541) ntplfle,ninsfle+1,trim(cline(right_word(2)+1:))
541     format(2i7,4x,a)

! -- Data is transferred up intil the "* observation groups" section.

        do
          iline=iline+1
          read(inunit,'(a)',err=9600,end=560) cline
          write(outunit,'(a)') trim(cline)
          call casetrans(cline,'lo')
          if(index(cline,'* observation gr').ne.0) go to 573
        end do
560     write(amessage,570) trim(infile)
570     format(' Cannot find "observation groups" section of PEST control file ',a,'.')
        go to 9890
573     continue
        do i=1,nobsgp
          iline=iline+1
          read(inunit,'(a)',err=9600,end=9650) cline
          write(outunit,'(a)') trim(cline)
          call linesplit(ifail,1)
          if(ifail.ne.0) go to 9600
          atemp=cline(left_word(1):right_word(1))
          call casetrans(atemp,'lo')
          if(atemp.eq.agroup)then
            write(amessage,575) trim(agroup),trim(infile)
575         format(' The observation group "',a,'" is already cited in existing ', &
            'PEST control file ',a,'.')
            go to 9890
          end if
        end do
        write(outunit,580) trim(agroup)
580     format(1x,a)

! -- And now the "* observation data" section is read and written.

        iline=iline+1
        read(inunit,'(a)',err=9600,end=9650) cline
        call casetrans(cline,'lo')
        if(index(cline,'* observation da').eq.0)then
          write(amessage,590) trim(infile)
590       format(' "* observation data" header missing or in wrong place in existing ',  &
          'PEST control file ',a,'.')
          go to 9890
        end if
        write(outunit,591)
591     format('* observation data')
        do i=1,nnobs
          iline=iline+1
          read(inunit,'(a)',err=9600,end=9650) cline
          write(outunit,'(a)') trim(cline)
        end do
	jindex=0
	do i=1,nbore
	  do j=1,numsamp(i)
	    jindex=jindex+1
	    if(ause.eq.'n')then
	      call num2char(jindex,aobs)
              aobs=trim(aprefix)//trim(aobs)
	    else
              if(aid.eq.'f')then
                ii1=1
                jj1=min(nnchar,len_trim(boreid(i)))
              else
                jj1=len_trim(boreid(i))
                ii1=max(1,jj1-nnchar+1)
              end if
              write(afmt,410) nnnchar,nnnchar
              write(atemp,afmt) j
              atemp=adjustl(atemp)
              aobs=trim(aprefix)//boreid(i)(ii1:jj1)//'_'//trim(atemp)
            end if
	    call casetrans(aobs,'lo')
            write(outunit,740) trim(aobs),value(jindex),trim(agroup)
740	    format(1x,a,t23,2x,1pg14.7,2x,'1.0  ',a)
          end do
        end do

! -- And now the model command line section.

        iline=iline+1
        read(inunit,'(a)') cline
        call casetrans(cline,'lo')
        if(index(cline,'command').eq.0)then
          call num2char(iline,aline)
          write(amessage,750) trim(aline),trim(infile)
750       format(' "* model command line" or "* derivatives command line" section header ',  &
          'expected at line ',a,' of file ',a,'.')
          go to 9890
        end if
        write(outunit,751)
751     format('* model command line')
        do
          iline=iline+1
          read(inunit,'(a)',err=9600,end=780) cline
          write(outunit,'(a)') trim(cline)
          call casetrans(cline,'lo')
          if(index(cline,'* model in').ne.0) go to 800
        end do
780     write(amessage,790) trim(infile)
790     format(' Cannot find "* model input/output" section header in file ',a,'.')
        go to 9890
800     do i=1,ntplfle
          read(inunit,'(a)',err=9600,end=9650) cline
          write(outunit,'(a)') trim(cline)
        end do
        do i=1,ninsfle
          read(inunit,'(a)',err=9600,end=9650) cline
          write(outunit,'(a)') trim(cline)
        end do
        call addquote(insfle,afile)
        call addquote(sampfile2,bfile)
        write(outunit,810) trim(afile),trim(bfile)
810     format(1x,a,2x,a)

! -- The remainder of the file is written.

        do
          iline=iline+1
          read(inunit,'(a)',err=9600,end=850) cline
          write(outunit,'(a)') trim(cline)
        end do
850     close(unit=inunit)
        write(6,*)
        write(6,860) trim(infile)
860     format(' - file ',a,' read ok.')
        close(unit=outunit)
        write(6,870) trim(outfile)
870     format(' - file ',a,' written ok.')
        write(6,880)
880     format(/,' Warning: always check a PEST input dataset with PESTCHEK before ',  &
        'running PEST.')

	go to 9900

9000	call num2char(iline,aline)
	write(amessage,9010) trim(aline),trim(sampfile2)
9010	format(' Error reading line ',a,' of bore sample file ',a)
	call write_message(leadspace='yes')
	go to 9900
9100	write(amessage,9110)
9110	format(' Cannot allocate sufficient memory to continue ',&
	'execution.')
	call write_message(leadspace='yes')
	go to 9900
9150	write(amessage,9160) trim(sampfile2)
9160	format(' Cannot close bore sample file ',a)
	go to 9890
9200	write(amessage,9210)
9210	format(' Memory management error.')
	go to 9890
9300	write(amessage,9310) trim(insfle)
9310	format(' Cannot write to file ',a)
	go to 9890
9600    call num2char(iline,aline)
        write(amessage,9610) trim(aline),trim(infile)
9610    format(' Error reading line ',a,' of PEST control file ',a,'.')
        go to 9890
9650    write(amessage,9660) trim(infile)
9660    format(' Unexpected end encountered to PEST control file ',a,'.')
        go to 9890


9890	call write_message(leadspace='yes')
        inquire(unit=outunit,opened=lopened)
        if(lopened)then
          close(unit=outunit,status='delete')
        end if
9900    call close_files
	deallocate(boreid,bore_index,numsamp,ndays,nsecs,  &
	value,stat=ierr)
	write(6,*)

end program pestprep2

