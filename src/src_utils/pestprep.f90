!     Last change:  JD    7 Oct 2006   12:23 pm
program pestprep

! -- Program PESTPREP prepares a PEST control file and a PEST instruction
!    file using pre-existing template files as well as a bore sample file
!    prepared on a preceding MOD2OBS or SMP2SMP run.


	use defn
	use inter

	implicit none

	integer, parameter :: MAXPAR=5000

	logical :: active
	integer	:: ifail,sampunit1,sampunit2,iobs,nobs,maxobs,iline,idate, &
	           nbore,ibore,ierr,cols,jindex,i,j,ii1,ii2,jj1,jj2,nnchar, &
	           insunit,blankflag,nnnchar,numtemp,tempunit,npar,nnpar,   &
	           j1,j2,ipar,ll,jj,kk,dd,ss,iichar,npargp,outunit,iheader,nbb
	integer :: numpargp(27)
	integer, allocatable    :: numsamp(:),bore_index(:),ndays(:),nsecs(:)
	real, allocatable       :: value(:)
	double precision        :: dtemp
	character (len=120)	:: sampfile1,sampfile2,specfile,insfle,outfle, &
                                   afile,bfile,modcom
	character (len=120), allocatable	:: tempfle(:),modfle(:)
	character (len=10)	:: abore,aboreold
	character (len=6)	:: aline
	character (len=10), allocatable    :: boreid(:)
	character (len=1)       :: ause,aid,pardelim,achar
	character (len=12)      :: aachar,atemp,aapar,apar(MAXPAR)
        character (len=20)      :: aobs
	character (len=15)      :: afmt
	character (len=100)	:: aprompt
	character (len=2000)    :: ccline


	write(amessage,5)
5       format(' Program PESTPREP prepares a PEST control file and ', &
	'PEST instruction files based on pre-existing template files and ',&
	'a bore sample file prepared by MOD2OBS or SMP2SMP.')
	call write_message(leadspace='yes',endspace='yes')

        call read_settings(ifail,idate,iheader)
        if(ifail.eq.1) then
          write(amessage,7)
7         format(' A settings file (settings.fig) was not found in the ', &
          'current directory.')
          call write_message
          go to 9900
        else if(ifail.eq.2) then
          write(amessage,8)
8         format(' Error encountered while reading settings file settings.fig')
          call write_message
          go to 9900
        endif
        if((idate.ne.0).or.(datespec.eq.0)) then
          write(amessage,9)
9         format(' Cannot read date format from settings file ', &
          'settings.fig')
          call write_message
          go to 9900
        end if

	call readfig(specfile,bore_coord_file,sampfile1)

70	call open_named_input_file(ifail, &
	' Enter name of observation bore sample file: ',sampfile1,sampunit1)
	if(ifail.ne.0) go to 9900
	if(escset.ne.0) go to 9900


80	call open_input_file(ifail, &
	' Enter name of    model    bore sample file: ',sampfile2,sampunit2)
	if(ifail.ne.0) go to 9900
	if(escset.ne.0)then
	  escset=0
	  close(unit=sampunit1)
	  write(6,*)
	  go to 70
	end if	

	blankflag=0
	aboreold=' '
	nobs=0
	maxobs=0
	iobs=0
	iline=0
	nbore=0
	do
	  iline=iline+1
	  read(sampunit2,'(a)',end=200,err=9000) cline
	  call linesplit(ifail,1)
	  if(ifail.lt.0) then
	    blankflag=1
	    cycle
	  end if
	  if(blankflag.eq.1) then
	    call num2char(iline-1,aline)
	    write(amessage,90) trim(aline),trim(sampfile2)
90	    format(' Line ',a,' of model-generated bore sample file ', &
	    a,' is blank: thus it can''t have been generated by ',&
	    'MOD2OBS or SMP2SMP.')
	    go to 9890
	  end if
	  abore=cline(left_word(1):right_word(1))
	  call casetrans(abore,'hi')
	  nobs=nobs+1
	  iobs=iobs+1
	  if(abore.ne.aboreold)then
	    if(iobs-1.gt.maxobs)maxobs=iobs-1
	    aboreold=abore
	    iobs=1
	    nbore=nbore+1
	  end if
	end do
200	if(iobs.gt.maxobs)maxobs=iobs

!	open(unit=50,file='debug.dat')
!	write(50,*) ' maxobs = ',maxobs
!	write(50,*) ' nobs   = ',nobs
!	write(50,*) ' nbore  = ',nbore
!	close(unit=50)

! -- Next we re-read the model bore sample file to ascertain bore_identifiers.

	allocate(boreid(nbore),bore_index(nbore),numsamp(nbore), &
	ndays(nobs),nsecs(nobs),value(nobs),stat=ierr)
	if(ierr.ne.0) go to 9100
	numsamp=0
	value=-1.1e37

	rewind(unit=sampunit2,iostat=ierr)
	if(ierr.ne.0)then
	  write(amessage,250) trim(sampfile2)
250	  format(' Cannot re-wind bore sample file ',a)
	  go to 9890
	end if
	aboreold=' '
	ibore=0
	jindex=0
	iline=0
	do
	  iline=iline+1
	  read(sampunit2,'(a)',end=300,err=9000) cline
	  call linesplit(ifail,5)
	  if(ifail.lt.0) cycle
	  if(ifail.eq.0)then
	    call num2char(iline,aline)
	    write(amessage,270) trim(aline),trim(sampfile2)
270	    format(' Line ',a,' of model-generated bore sample file ', &
	    a,' has 5 entries: thus it can''t have been generated by ',&
	    'MOD2OBS or SMP2SMP.')
	    go to 9890
	  end if
	  call linesplit(ifail,4)
	  if(ifail.ne.0)then
	    call num2char(iline,aline)
	    write(amessage,280) trim(aline),trim(sampfile2)
280	    format(' Line ',a,' of model-generated bore sample file ', &
	    a,' has less than 4 entries: this can''t have been generated by ', &
	    'MOD2OBS or SMP2SMP.')
	    go to 9890
	  end if
	  cols=4
	  jindex=jindex+1
	  abore=cline(left_word(1):right_word(1))
	  call casetrans(abore,'hi')
	  if(abore.ne.aboreold)then
	    aboreold=abore
	    ibore=ibore+1
	    boreid(ibore)=abore
	    bore_index(ibore)=jindex
	  end if
	  numsamp(ibore)=numsamp(ibore)+1
	  call read_rest_of_sample_line(ifail,cols,ndays(jindex),nsecs(jindex), &
	  dtemp,iline,sampfile2)
	  if(ifail.ne.0) go to 9900
	end do
300	continue

!	open(unit=50,file='debug.dat')	!debug
!	do i=1,nbore                    !debug
!	  write(50,*) trim(boreid(i)), bore_index(i), numsamp(i)
!	end do                          !debug
!	write(50,*)                     !debug
!	do i=1,nobs                     !debug
!	  write(50,*) ndays(i),nsecs(i) !debug
!	end do                          !debug
!	close (unit=50)	                !debug

305	write(6,*)
310     write(6,320,advance='no')
320     format(' Use numbers or bore identifiers for observation ', &
        'names?  [n/b]: ')
        read(5,'(a)') ause
        if(ause.eq.' ') go to 310
        if(index(eschar,ause).ne.0) then
          close(unit=sampunit2,err=9150)
	  deallocate(boreid,bore_index,numsamp,ndays,nsecs,value,stat=ierr)
	  if(ierr.ne.0) go to 9200
	  write(6,*)
	  go to 80
        end if
        call casetrans(ause,'lo')
        if((ause.ne.'n').and.(ause.ne.'b')) go to 310
        if(ause.eq.'b')then
          if(maxobs.gt.99999)then
            write(amessage,330)
330         format(' There are too many observations for this option.')
            call write_message
            go to 310
          end if
          if(maxobs.gt.100000)then
            nnnchar=6
          else if(maxobs.gt.10000)then
            nnnchar=5
          else if(maxobs.ge.1000)then
            nnnchar=4
          else if(maxobs.ge.100)then
            nnnchar=3
          else if(maxobs.ge.10)then
            nnnchar=2
          else
            nnnchar=1
          end if
          nnchar=20-nnnchar-1
          write(aachar,'(i8)') nnchar
          aachar=adjustl(aachar)
340       write(6,350,advance='no') trim(aachar),trim(aachar)
350       format(' Use first ',a,' or last ',a,' characters ',      &
          'of bore identifier?  [f/l]: ')
          read(5,'(a)') aid
          if(aid.eq.' ') go to 340
          if(index(eschar,aid).ne.0) go to 305
          call casetrans(aid,'lo')
          if((aid.ne.'f').and.(aid.ne.'l')) go to 340
	  if(nbore.ne.1)then
            if(aid.eq.'f') then
              do i=1,nbore-1
                do j=i+1,nbore
                  if(boreid(i)(1:nnchar).eq.boreid(j)(1:nnchar)) then
                    write(amessage,360)
360                 format(' This will not result in unique observation ', &
                    'names.')
                    call write_message(leadspace='yes',endspace='yes')
                    go to 310
                  end if
                end do
              end do
            else
              do i=1,nbore-1
                do j=i+1,nbore
                  jj1=len_trim(boreid(i))
                  ii1=max(1,jj1-nnchar+1)
                  jj2=len_trim(boreid(j))
                  ii2=max(1,jj2-nnchar+1)
                  if(boreid(i)(ii1:jj1).eq.boreid(j)(ii2:jj2))then
                    write(amessage,360)
                    call write_message(leadspace='yes',endspace='yes')
                    go to 310
                  end if
                end do
	      end do
	    end if
	  end if
	end if


400	write(6,*)
	call open_output_file(ifail,  &
        ' Enter name for instruction file: ', insfle,insunit)
	if(escset.ne.0)then
	  escset=0
	  write(6,*)
	  go to 310
	end if
	jindex=0
	write(insunit,405)
405	format('pif #')
	do i=1,nbore
	  do j=1,numsamp(i)
	    jindex=jindex+1
	    if(ause.eq.'n')then
	      call num2char(jindex,aobs)
	    else
              if(aid.eq.'f')then
                ii1=1
                jj1=min(nnchar,len_trim(boreid(i)))
              else
                jj1=len_trim(boreid(i))
                ii1=max(1,jj1-nnchar+1)
              end if
              write(afmt,410) nnnchar,nnnchar
410           format('(i',i5,'.',i5,')')
              write(atemp,afmt) j
              atemp=adjustl(atemp)
              aobs=boreid(i)(ii1:jj1)//'_'//trim(atemp)
            end if
	    call casetrans(aobs,'lo')
            write(insunit,420,err=9300) trim(aobs)
420	    format('l1   [',a,']39:56')
          end do
        end do
	close(unit=insunit)
	write(amessage,430) trim(insfle)
430	format(' - file ',a,' written ok.')
	call write_message

! -- Next we find out the names of all parameters by reading template files.

505	write(6,*)
500	write(6,510,advance='no')
510	format(' How many template files are there? ')
	if(key_read(numtemp).ne.0) go to 500
	if(escset.ne.0)then
	  escset=0
	  go to 400
	end if
	if(numtemp.le.0) go to 500
	allocate(tempfle(numtemp),modfle(numtemp),stat=ierr)
	if(ierr.ne.0) go to 9100
	i=0
	npar=0
	read_template_file:do 
	  nnpar=0
	  i=i+1
	  if(i.gt.numtemp) exit
520	  call num2char(i,aline)
	  aprompt=' Enter name for template file # '//trim(aline)//': '
	  call open_input_file(ifail,aprompt,tempfle(i),tempunit)
	  if(ifail.ne.0) go to 9900
	  if(escset.ne.0)then
	    escset=0
	    deallocate(tempfle,modfle,stat=ierr)
	    if(ierr.ne.0) go to 9200
	    go to 505
	  end if
512       write(6,511,advance='no')
511       format(' Enter name of corresponding model input file: ')
          read(5,'(a)') modfle(i)
          if(modfle(i).eq.' ') go to 512
          modfle(i)=adjustl(modfle(i))
          if(index(eschar,trim(modfle(i))).ne.0) then
            close(unit=tempunit)
            go to 520
          end if
          nbb=len_trim(modfle(i))
          call getfile(ifail,modfle(i),bfile,1,nbb)
          if(ifail.ne.0) go to 512
          modfle(i)=bfile
	  cline=' '
	  read(tempunit,'(a)',err=9350,end=600) cline	    
	  if(cline(1:3).ne.'ptf')then
	    write(amessage,530) trim(tempfle(i))
530	    format(' "ptf" header missing from first line of file ',a)
	    call write_message(leadspace='yes',endspace='yes')
	    close(unit=tempunit)
	    go to 520
	  end if
	  pardelim=cline(5:5)
	  if((pardelim.eq.' ').or.   &
	     (index('1234567890,;:',pardelim).ne.0).or.    &
	     (index('abcdefghijklmnopqrstuvwxyz',pardelim).ne.0))then
	     write(amessage,540) trim(tempfle(i))
540	     format(' Invalid parameter delimeter on line 1 of file ',a)
	     call write_message(leadspace='yes',endspace='yes')
	     close(unit=tempunit)
	     go to 520
	  end if
	  iline=1
	  read_a_line:do
	    ii1=1
	    iline=iline+1
	    ccline=' '
	    read(tempunit,'(a)',err=9350,end=600) ccline
            ll=len(ccline)
545	    j=index(ccline(ii1:),pardelim)
	    if(j.eq.0) cycle read_a_line
            if(j.gt.ll) cycle read_a_line
	    ii1=ii1+j-1
	    j=0
	    if(ii1.le.ll)j=index(ccline(ii1+1:),pardelim)
	    if(j.eq.0)then
	      call num2char(iline,aline)
	      write(amessage,550) trim(aline),trim(tempfle(i))
550	      format(' Unbalanced parameter delimiters on line ',a,  &
	      ' of file ',a)
	      go to 9890
	    end if
	    jj1=ii1+j
	    ii1=ii1+1
	    jj1=jj1-1
	    if(jj1-ii1+1.le.0)then
	      call num2char(iline,aline)
	      write(amessage,560) trim(aline),trim(tempfle(i))
560	      format(' Parameter space has zero width at line ',a,   &
	      ' of file ',a)
	      go to 9890
	    end if
	    do jj=ii1,jj1
	      if(ccline(jj:jj).ne.' ') then
	        do kk=jj,jj1
	          if(ccline(kk:kk).eq.' ') go to 565
	        end do
	        kk=jj1+1
565	        kk=kk-1
	        go to 567
	      end if
	    end do
	    call num2char(iline,aline)
	    write(amessage,566) trim(aline), trim(tempfle(i))
566	    format(' Blank parameter space at line ',a,' of template ', &
	    'file ',a)
	    go to 9890
567	    continue
	    if(kk-jj+1.gt.12)then
	      call num2char(iline,aline)
	      write(amessage,568) trim(aline),trim(tempfle(i))
568	      format(' Parameter name greater than 12 characters in ',  &
	      'line ',a,' of template file ',a)
	      go to 9890
	    end if
	    aapar=ccline(jj:kk)
	    aapar=adjustl(aapar)
	    call casetrans(aapar,'lo')
	    if(npar.ne.0)then
	      do ipar=1,npar
	        if(aapar.eq.apar(ipar)) go to 585
	      end do
	      npar=npar+1
	      nnpar=nnpar+1
	      if(npar.gt.MAXPAR)then
	        write(amessage,580)
580	        format(' Too many parameters - increase MAXPAR ',  &
	        'and re-compile program PESTPREP.')
	        go to 9890
	      end if
	      apar(npar)=aapar
	    else
	      npar=1
	      nnpar=1
	      apar(npar)=aapar
	    end if
585	    ii1=jj1+2
	    go to 545
	  end do read_a_line
600	  continue
	  call num2char(nnpar,aline)
	  if(i.eq.1)then
	    write(amessage,590) trim(aline),trim(tempfle(i))
590	    format(' - ',a,' parameters read from file ',a)
	  else
	    write(amessage,595) trim(aline),trim(tempfle(i))
595	    format(' - ',a,' more parameters read from file ',a)
	  end if
	  call write_message
	  close(unit=tempunit,err=9400)
	end do read_template_file

!	open(unit=50,file='debug.dat')		!debug
!	do i=1,npar				!debug
!	  write(50,*) apar(i)			!debug
!	end do					!debug
!	close(unit=50)				!debug

! -- Next we find out the measured values pertaining to all observations.

	iline=0
	aboreold=' '
	active=.false.
	read_meas_sample_file: do
	  iline=iline+1
	  read(sampunit1,'(a)',end=650) cline
	  call linesplit(ifail,1)
	  if(ifail.lt.0) cycle read_meas_sample_file
	  abore=cline(left_word(1):right_word(1))
	  call casetrans(abore,'hi')
	  if(abore.eq.aboreold)then
	    if(.not.active) cycle read_meas_sample_file
	  else
	    aboreold=abore
	    do i=1,nbore
	      if(abore.eq.boreid(i)) go to 620
	    end do
	    active=.false.
	    cycle read_meas_sample_file
620	    active=.true.
	  end if
	  call linesplit(ifail,4)
	  if(ifail.gt.0)then
	    call num2char(iline,aline)
	    write(amessage,630) trim(aline),trim(sampfile1)
630	    format(' Insufficient entries on line ',a,' of bore ', &
	    'sample file ',a)
	    go to 9890
	  end if
	  cols=4
	  call read_rest_of_sample_line(ifail,cols,dd,ss,dtemp,iline,sampfile1)
	  if(ifail.ne.0) go to 9900
	  do j=bore_index(i),bore_index(i)+numsamp(i)-1
	    if((dd.eq.ndays(j)).and.(ss.eq.nsecs(j)))then
	      value(j)=dtemp
	      if(j.ne.nobs)then
	        if(value(j+1).gt.-1.0e37)then
	          write(amessage,635) trim(sampfile1),trim(sampfile2)
635	          format(' Samples in file ',a,' are in a different order ', &
	          'from those in file ',a,': this is not possible if the ',  &
	          'latter was built from the former using MOD2OBS or SMP2SMP.')
                  write(6,*) 'iline = ',iline   !debug
	          go to 9890
	        end if
	      end if
	      go to 640
	    end if
	  end do
640	  continue
	end do read_meas_sample_file
650	continue

	do i=1,nobs
	  if(value(i).lt.-1.0e37)go to 660
	end do
	go to 700
660	write(amessage,670) trim(sampfile2),trim(sampfile1)
670	format(' There are one or a number of samples supplied in file ',a,  &
	' for which there are no corresponding samples in file ',a)
	call write_message(leadspace='yes')
	write(amessage,680) trim(sampfile2), trim(sampfile1)
680	format(' This would not have happened if file ',a,' were built from ', &
	'file ',a,' using MOD2OBS or SMP2SMP as it should have been.')
	call write_message(endspace='yes')
	go to 9900

! -- Next we work out the number of parameter groups.

700	numpargp=0
	do i=1,npar
	  iichar=ichar(apar(i)(1:1))
	  if((iichar.ge.97).and.(iichar.le.122)) then
	    numpargp(iichar-96)=numpargp(iichar-96)+1
	  else
	    numpargp(27)=numpargp(27)+1
	  end if
	end do
	npargp=0
	do i=1,27
	  if(numpargp(i).ne.0) npargp=npargp+1
	end do

!	open(unit=50,file='debug.dat')       !debug
!	write(50,*) npargp                   !debug
!	write(50,*)                          !debug
!	do i=1,27                            !debug
!	  write(50,*) numpargp(i)            !debug
!	end do                               !debug
!	close (unit=50)                      !debug


! -- We now write the PEST control file.

	write(6,*)
719	call open_output_file(ifail, &
	' Enter name for output PEST control file: ',outfle,outunit)
	if(ifail.ne.0) go to 9900
	if(escset.ne.0)then
	  escset=0
	  deallocate(tempfle,modfle,stat=ierr)
	  if(ierr.ne.0) go to 9200
	  go to 505
	end if
723     write(6,722,advance='no')
722     format(' Enter command which PEST will use to run model: ')
        read(5,'(a)') modcom
        if(modcom.eq.' ') go to 723
	modcom=adjustl(modcom)
	if(index(eschar,modcom(1:2)).ne.0) then
          close(unit=outunit)
	  go to 719
        end if
        nbb=len_trim(modcom)
        call getfile(ifail,modcom,bfile,1,nbb)
        if(ifail.ne.0) go to 723
        modcom=bfile
	write(outunit,720,err=9500)
720	format('pcf')
	write(outunit,730,err=9500)
730	format('* control data')
	write(outunit,740,err=9500)
740	format('restart  estimation')
	write(outunit,750,err=9500) npar,nobs,npargp,0,1
750	format(1x,5i8)
	write(outunit,760,err=9500) numtemp,1
760	format(1x,i5,1x,i5,' single  point  1   0   0')
	write(outunit,770,err=9500)
770	format(' 10.0  2.0  0.3  0.03  10')
	write(outunit,780,err=9500)
780	format(' 10.0  10.0  0.001')
	write(outunit,790,err=9500)
790	format(' 0.1  noaui')
	write(outunit,800,err=9500)
800	format(' 30  0.005  4  4  0.005  4')
	write(outunit,810,err=9500)
810	format(' 1  1  1')
	write(outunit,820,err=9500)
820	format('* parameter groups')
	do i=1,26
	  if(numpargp(i).ne.0)then
	    achar=char(i+96)
	    if(achar.eq.'r')then
	      write(outunit,830,err=9500) achar
830	      format(1x,a,5x,'rel_to_max',2x,'0.01  0.0  switch  2.0 parabolic')
	    else
	      write(outunit,840,err=9500) achar
840	      format(1x,a,5x,'relative  ',2x,'0.01  0.0  switch  2.0 parabolic')
	    end if
	  end if
	end do
	if(numpargp(27).ne.0)then
	  write(outunit,850,err=9500)
850	  format(1x,'other relative    0.01  0.0  switch  2.0 parabolic')
	endif
	write(outunit,860,err=9500)
860	format('* parameter data')
	do i=1,npar
	  achar=apar(i)(1:1)
	  iichar=ichar(achar)
	  if((achar.eq.'t').or.(achar.eq.'h').or.(achar.eq.'v').or.  &
	    (achar.eq.'k').or.(achar.eq.'p'))then 
	    write(outunit,870,err=9500) apar(i),achar
870	    format(1x,a12,1x,'log  factor   1.0  1.0e-10  1.0e10 ',a,'     1.0  0.0  1')
	  else if(achar.eq.'s')then
	    write(outunit,875,err=9500) apar(i),achar
875	    format(1x,a12,1x,'log  factor   0.1  1.0e-10  0.3    ',a,'     1.0  0.0  1')
	  else if((iichar.ge.97).and.(iichar.le.122))then
	    write(outunit,880,err=9500) apar(i),achar
880	    format(1x,a12,1x,'none relative 1.0  -1.0e10  1.0e10 ',a,'     1.0  0.0  1')
	  else
	    write(outunit,885,err=9500) apar(i)
885	    format(1x,a12,1x,'none relative 1.0  -1.0e10  1.0e10 other 1.0  0.0  1')
	  end if
	end do
	write(outunit,890,err=9500)
890	format('* observation groups')
	write(outunit,900,err=9500)
900	format(' obsgroup')
	write(outunit,910,err=9500)
910	format('* observation data')
	jindex=0
	do i=1,nbore
	  do j=1,numsamp(i)
	    jindex=jindex+1
	    if(ause.eq.'n')then
	      call num2char(jindex,aobs)
	    else
              if(aid.eq.'f')then
                ii1=1
                jj1=min(nnchar,len_trim(boreid(i)))
              else
                jj1=len_trim(boreid(i))
                ii1=max(1,jj1-nnchar+1)
              end if
              write(afmt,410) nnnchar,nnnchar
              write(atemp,afmt) j
              atemp=adjustl(atemp)
              aobs=boreid(i)(ii1:jj1)//'_'//trim(atemp)
            end if
	    call casetrans(aobs,'lo')
            write(outunit,920,err=9500) trim(aobs),value(jindex)
920	    format(1x,a,t23,2x,1pg14.7,2x,'1.0  obsgroup')
          end do
        end do
	write(outunit,930,err=9500)
930	format('* model command line')
        call addquote(modcom,afile)
	write(outunit,940,err=9500) trim(afile)
940	format(1x,a)
	write(outunit,950,err=9500)
950	format('* model input/output')
	do i=1,numtemp
          call addquote(tempfle(i),afile)
          call addquote(modfle(i),bfile)
	  write(outunit,960,err=9500) trim(afile),trim(bfile)
960	  format(1x,a,2x,a)
	end do
        call addquote(insfle,afile)
        call addquote(sampfile2,bfile)
	write(outunit,970,err=9500) trim(afile),trim(bfile)
970	format(1x,a,2x,a)
	write(outunit,980)
980	format('* prior information')
	close(unit=outunit,iostat=ierr)
	write(amessage,990) trim(outfle)
990	format(' - file ',a,' written ok.')
	call write_message

	go to 9900

9000	call num2char(iline,aline)
	write(amessage,9010) trim(aline),trim(sampfile2)
9010	format(' Error reading line ',a,' of bore sample file ',a)
	call write_message(leadspace='yes')
	go to 9900
9100	write(amessage,9110)
9110	format(' Cannot allocate sufficient memory to continue ',&
	'execution.')
	call write_message(leadspace='yes')
	go to 9900
9150	write(amessage,9160) trim(sampfile2)
9160	format(' Cannot close bore sample file ',a)
	go to 9890
9200	write(amessage,9210)
9210	format(' Memory management error.')
	go to 9890
9300	write(amessage,9310) trim(insfle)
9310	format(' Cannot write to file ',a)
	go to 9890
9350	write(amessage,9360) trim(tempfle(i))
9360	format(' Error encountered in reading template file ',a)
	go to 9890
9400	write(amessage,9410) trim(tempfle(i))
9410	format(' Cannot close template file ',a)
	go to 9890
9500	write(amessage,9510) trim(outfle)
9510	format(' Error writing to file ',a)
	go to 9890



9890	call write_message(leadspace='yes')
9900    call close_files
	deallocate(boreid,bore_index,numsamp,ndays,nsecs,  &
	tempfle,modfle,value,stat=ierr)
	write(6,*)

end program pestprep

