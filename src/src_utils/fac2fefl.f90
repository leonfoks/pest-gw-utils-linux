!     Last change:  JD   12 Feb 2001    2:26 pm

program fac2fefl

! -- Program FAC2FEFL modifies a FEFLOW input file, replacing element values for a particular
!    property with those interpolated from pilot points using kriging factors generated by the
!    PPK2FAC_FEFL utility.

        use defn
        use inter

        implicit none

        integer, parameter    :: MAXENT=100
        integer               :: ifail,facunit,ierr,ncol,nrow,nerr,nbb, &
                                 icellno,na,icol,irow,i,itrans,nnppt,outunit,ielem
        integer               :: irep,iline,nn,ii,j,jj,iend,ifin,istart,inunit
        integer               :: carrydash,carrynode,idash,ndash
        integer, allocatable, dimension(:)  :: ipt
        integer, allocatable, dimension(:)  :: recelem_f(:)
        integer, dimension(MAXENT)          :: dashpos(MAXENT),inode(MAXENT),inodedash(MAXENT)
        real                                :: backval,rtemp,sum,rtemp1,rlo,rhi,rtemp2
        real, allocatable, dimension(:)     :: wt
        double precision                    :: dval,startval
        double precision, allocatable, dimension(:) :: tempelem_f
        character (len=1)                   :: facformat,arealformat,alimit
        character (len=5)                   :: acode
        character (len=10)                  :: aline
        character (len=20)                  :: atemp
        character (len=20)                  :: datname
        character (len=200)                 :: aprompt,facfile,afile,intfile,outfile,infile
        character (len=500)                 :: dline
        character (len=12), allocatable, dimension(:)    :: wpoints


        write(amessage,5)
5        format(' Program FAC2FEFL carries out spatial interpolation for a FEFLOW model based on ', &
        'interpolation factors calculated by PPK2FAC_FEFL and pilot point values contained ', &
        'in a pilot points file.')
        call write_message(leadspace='yes',endspace='yes')

! -- The first two lines of the interpolation factor file are read.

10      aprompt=' Enter name of interpolation factor file: '
        call open_input_file(ifail,aprompt,facfile,facunit,form_prompt='yes', &
        fformat=facformat)
        if(ifail.ne.0) go to 9900
        if(escset.ne.0) go to 9900

        if(facformat.eq.'f')then
          read(facunit,'(a)',err=9000,end=9100) afile
        else
          read(facunit,err=9000,end=9100) afile
        end if
        afile=adjustl(afile)
        nbb=len_trim(afile)
        call getfile (ifail,afile,pilot_points_file,1,nbb)
        if(ifail.ne.0)pilot_points_file=' '

        if(facformat.eq.'f')then
          read(facunit,'(a)',err=9000,end=9100) afile
        else
          read(facunit,err=9000,end=9100) afile
        end if
        afile=adjustl(afile)
        if(afile.eq.' ')then
          intfile=' '
        else
          nbb=len_trim(afile)
          call getfile (ifail,afile,intfile,1,nbb)
          if(ifail.ne.0)intfile=' '
        end if

        if(facformat.eq.'f')then
          read(facunit,*,err=9000,end=9100) numelem_f
        else
          read(facunit,err=9000,end=9100) numelem_f
        end if
        allocate(datelem_f(numelem_f),recelem_f(numelem_f),tempelem_f(numelem_f),stat=ierr)
        if(ierr.ne.0) then
          write(amessage,50)
50        format(' Cannot allocate sufficient memory to run FAC2FEFL.')
          go to 9890
        end if
        datelem_f=-1.1d37                ! An array

        if(facformat.eq.'f')then
          read(facunit,*,err=9000,end=9100) nnppt
        else
          read(facunit,err=9000,end=9100) nnppt
        end if
        allocate(wpoints(nnppt),stat=ierr)
        if(ierr.ne.0) then
          write(amessage,50)
          go to 9890
        end if
        if(facformat.eq.'f')then
          do i=1,nnppt
            read(facunit,'(a)',err=9000,end=9100) wpoints(i)
          end do
        else
          do i=1,nnppt
            read(facunit,err=9000,end=9100) wpoints(i)
          end do
        end if
        do i=1,nnppt
          wpoints(i)=adjustl(wpoints(i))
          call casetrans(wpoints(i),'hi')
        end do

! -- The pilot points file is read.

        write(6,*)
80      call read_pilot_points_file(ifail, &
        ' Enter name of pilot points file: ')
        if(ifail.ne.0) go to 9900
        if(escset.ne.0) then
          escset=0
          deallocate(datelem_f,recelem_f,wpoints,tempelem_f,stat=ierr)
          if(ierr.ne.0) then
            write(amessage,57)
57          format(' Memory management error: cannot continue execution.')
            go to 9890
          end if
          close(unit=facunit)
          write(6,*)
          go to 10
        end if
        if(nnppt.ne.num_pilot_points)then
          write(amessage,59)
          go to 9890
        else
          do i=1,num_pilot_points
            if(pilot_point_id(i).ne.wpoints(i))then
              write(amessage,59)
59            format(' The pilot points in the pilot points file are not the same, ', &
              'or are not arranged in the same order, as the pilot points in the ', &
              'pilot points file read by PPK2FAC_FEFL when it calculated the ', &
              'interpolation factors contained in the factor file.')
              go to 9890
            end if
          end do
        end if

! -- Interpolation upper and lower limits are supplied.

        write(6,*)
270     write(6,280,advance='no')
280     format(' Enter lower interpolation limit: ')
        if(key_read(rlo).ne.0) go to 270
        if(escset.eq.1) then
          write(6,*)
          escset=0
          go to 80
        end if

310     write(6,320,advance='no')
320     format(' Enter upper interpolation limit: ')
        if(key_read(rhi).ne.0) go to 310
        if(escset.eq.1) then
          write(6,*)
          escset=0
          go to 270
        end if
        if(rlo.ge.rhi)then
          write(6,330)
330       format(/,' Upper limit must be greater than lower limit - try again.',/)
          go to 310
        end if

! -- The input and output FEM files are named.

        write(6,*)
340     call open_input_file(ifail, &
        ' Enter name of existing FEM file: ',infile,inunit)
	if(ifail.ne.0) go to 9900
	if(escset.ne.0) then
          escset=0
          write(6,*)
          go to 310
        end if
341     write(6,342,advance='no')
342     format(' Enter code for element list identification: ')
        read(5,'(a)') afile
        if(afile.eq.' ') go to 341
        afile=adjustl(afile)
        if((afile(1:2).eq.'e ').or.(afile(1:2).eq.'E '))then
          close(unit=inunit)
          write(6,*)
          go to 340
        end if
        nbb=len_trim(afile)
        call getfile (ifail,afile,atemp,1,nbb)
        if(ifail.ne.0) go to 341
        atemp=adjustl(atemp)
        nbb=len_trim(atemp)
        if((index(atemp(1:nbb),' ').ne.0).or.(index(atemp(1:nbb),char(9)).ne.0))then
          write(6,343)
343       format(/,' No spaces or tabs allowed in code - try again.',/)
          go to 341
        end if
        if(nbb.gt.5)then
          write(6,344)
344       format(/,' Code must be 5 characters or less - try again.',/)
          go to 341
        end if
        acode=atemp(1:nbb)
350     call open_output_file(ifail, &
        ' Enter name for new FEM file: ',outfile,outunit)
	if(ifail.ne.0) go to 9900
	if(escset.ne.0) then
          escset=0
          write(6,*)
          go to 341
        end if

! -- The interpolation factor file is now read line by line and the factors
!    are used to undertake spatial interpolation.

        allocate(ipt(num_pilot_points),wt(num_pilot_points),stat=ierr)
        if(ierr.ne.0) then
          write(amessage,50)
          go to 9890
        end if

        write(6,351)
351     format(/,' - interpolating from pilot points to finite element mesh...')

        do
          if(facformat.eq.'f')then
            read(facunit,*,err=9000,end=600) icellno,itrans, &
            na,rtemp,((ipt(i),wt(i)),i=1,na)
          else
            read(facunit,err=9000,end=600)   icellno,itrans, &
            na,rtemp,((ipt(i),wt(i)),i=1,na)
          end if
          sum=rtemp
          do i=1,na
            if(itrans.eq.0)then
              sum=sum+pilot_point_val(ipt(i))*wt(i)
            else
              rtemp2=pilot_point_val(ipt(i))
              if(rtemp2.le.0.0)then
                write(amessage,125)
125             format(' The interpolation factor file specifies that spatial ', &
                'interpolation in at least one zone takes place on the basis of the ', &
                'logarithms of pilot point values. However at least one of the ', &
                'pertinent pilot point values is negative.')
                go to 9890
              end if
              sum=sum+log10(rtemp2)*wt(i)
            end if
          end do
          if(itrans.eq.0)then
            rtemp1=sum
          else
            rtemp1=10**sum
          end if
          if(rtemp1.gt.rhi)then
            rtemp1=rhi
          else if(rtemp1.lt.rlo)then
            rtemp1=rlo
          end if
          datelem_f(icellno)=rtemp1
        end do
600     continue
        tempelem_f=datelem_f    ! Arrays

! -- Data is read from the input FEM file up until the next incidence of replacement code.
!    (Note that this process is repeated in case multiple element list replacements are required.)

        write(6,695)
695     format(' - reading existing FEM file and writing new one...')

        irep=0
        iline=0
700     continue
        do
          iline=iline+1
          read(inunit,'(a)',end=1000) cline
          nn=index(cline,trim(acode))
          if(nn.eq.0) then
            write(outunit,'(a)') trim(cline)
            cycle
          end if
          irep=irep+1
          if(irep.gt.1)then
            datelem_f=tempelem_f             ! arrays
          end if
          go to 705
        end do

! -- The line containing the list replacement code is checked for integrity.

705     continue
        call linesplit(ifail,3)
        if(ifail.ne.0) go to 9200
        atemp=cline(left_word(1):right_word(1))
        if(index(atemp,'.').eq.0) go to 9200
        do i=1,len_trim(acode)
          cline(nn+i-1:nn+i-1)=' '
        end do

! -- The element list is now read in its entirety.

        recelem_f=0                 ! An array
        carrydash=0
729     continue
        if((cline(1:1).ne.char(9)).or.(cline(2:2).ne.char(9)))then
          if(carrydash.ne.0)then
            call num2char(iline,aline)
            write(amessage,732) trim(aline),trim(infile)
732         format(' Unterminated node range at line ',a,' of file ',a,'.')
            go to 9890
          end if
          call linesplit(ifail,1)
          if(ifail.ne.0) go to 900
          atemp=cline(left_word(1):right_word(1))
          if(index(atemp,'.').eq.0) go to 900
          dval=char2double(ifail,1)
          if(ifail.ne.0) go to 900
          cline(left_word(1):right_word(1))=' '
        end if
        ndash=0
733     continue
        nn=index(cline,'-')
        if(nn.ne.0)then
          ndash=ndash+1
          if(ndash.gt.MAXENT) go to 900
          dashpos(ndash)=nn
          cline(nn:nn)=' '
          go to 733
        end if
        do nn=1,MAXENT
          call linesplit(ifail,nn)
          if(ifail.ne.0) go to 730
        end do
        go to 900
730     continue
        nn=nn-1
        do i=1,nn
          inode(i)=char2int(ifail,i)
          if(ifail.ne.0) go to 900
        end do
        inodedash=0           ! an array
        if(ndash.ne.0)then
          do idash=1,ndash
            do i=nn,1,-1
              if(dashpos(idash).gt.right_word(i))then
                inodedash(i)=1
                go to 740
              end if
            end do
            go to 900
740         continue
          end do
        end if
        i=1
741       continue
          ii=inode(i)
          if(ii.gt.numelem_f)then
            call num2char(iline,aline)
            write(amessage,742) trim(aline),trim(infile)
742         format(' Element number exceeds total number of model elements at line ',a,' of file ',a,'.')
            go to 9890
          end if
          if(carrydash.eq.1)then
            do j=carrynode,ii
              if(datelem_f(j).lt.-1.0d37) datelem_f(j)=dval
              recelem_f(j)=1
            end do
            carrydash=0
            i=i+1
          else
            if(inodedash(i).eq.0)then
              if(datelem_f(ii).lt.-1.0d37)datelem_f(ii)=dval
              recelem_f(ii)=1
              i=i+1
            else
              if(i.ne.nn)then
                jj=inode(i+1)
                if(jj.gt.numelem_f)then
                  call num2char(iline,aline)
                  write(amessage,742) trim(aline),trim(infile)
                  go to 9890
                end if
                do j=ii,jj
                  if(datelem_f(j).lt.-1.0d37) datelem_f(j)=dval
                  recelem_f(j)=1
                end do
                i=i+2
              else
                carrydash=1
                carrynode=ii
                i=i+1
              end if
            end if
          end if
          if(i.le.nn) go to 741
        continue
        iend=1
        iline=iline+1
        read(inunit,'(a)',end=900) cline
        dline=cline
        iend=0
        go to 729

900     continue
        if(carrydash.ne.0)then
          write(amessage,732) trim(aline),trim(infile)
          go to 9890
        end if

! -- The replacement node list is now written to the FEM file.

        i=1
910     continue
        if(recelem_f(i).eq.0) then
          ifin=i
          go to 945
        end if
        istart=i
        startval=datelem_f(i)
        if(i.eq.numelem_f)then
          ifin=i
        else
          do j=i+1,numelem_f
            if(recelem_f(j).eq.0) then
              ifin=j-1
              go to 920
            else if(.not.equals(startval,datelem_f(j))) then
              ifin=j-1
              go to 920
            end if
          end do
          ifin=numelem_f
        end if
920     if(istart.eq.ifin)then
          write(outunit,930) startval,istart
930       format(1x,ES14.6e3,2x,i8)
        else
          write(outunit,940) startval,istart,ifin
940       format(1x,ES14.6e3,2x,i8,'- ',i8)
        end if
945     continue
        i=ifin+1
        if(ifin.gt.numelem_f)go to 950
        go to 910

950     continue
        if(iend.eq.0) then
          write(outunit,'(a)') trim(dline)
          go to 700
        end if

1000    continue

! -- Tidying up.

        close(unit=inunit)
        close(unit=outunit)
        if(irep.eq.0)then
          write(amessage,1010) trim(acode),trim(infile)
1010      format(' No incidences of string "',a,'" found in file ',a,'.')
          go to 9890
        else
          write(6,*)
          write(6,1015) trim(infile)
1015      format(' - file ',a,' read ok.')
          write(6,1020) trim(outfile)
1020      format(' - file ',a,' written ok.')
        end if

        go to 9900

9000    write(amessage,9010) trim(facfile)
9010    format(' Error encountered in reading interpolation factor file ',a)
        go to 9890
9100    write(amessage,9110) trim(facfile)
9110    format(' Unexpected end encountered to file ',a)
        go to 9890
9200    call num2char(iline,aline)
        write(amessage,9210) trim(aline),trim(infile)
9210    format(' Node list replacement marker does not appear to lie on first line of a node ',  &
        'list at line ',a,' of file ',a,'.')
        go to 9890

9890    call write_message(leadspace='yes',endspace='yes')
9900    call close_files
        deallocate(datelem_f,recelem_f,ipt,wt,wpoints,tempelem_f,stat=ierr)


end program fac2fefl



